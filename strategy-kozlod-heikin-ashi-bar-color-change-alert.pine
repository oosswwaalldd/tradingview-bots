// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Ossy1717

//@version=4
strategy("(Strategy) Kozlod - Heikin-Ashi Bar Color Change Alert", overlay = true, initial_capital = 1000, default_qty_value = 1000, default_qty_type = strategy.cash)

// Inputs
fromDate = input(title="From Date", type=input.time, defval=timestamp("01 Jan 2021 00:00 -0400"))
showTrailingStopLine = input(defval = "Yes", title = "Show Trailing Stop Line", options = ["Yes", "No"])
trailingStopPerc = input(defval = 2, title = "Stop Loss %", minval = .5, maxval = 100, step = .5) / 100

// Calculation HA Values 
haopen   = 0.0
haclose  = ((open + high + low + close)/4)
haopen  := na(haopen[1]) ? (open + close)/2 : (haopen[1] + haclose[1]) / 2
hahigh   = max(high, max(haopen, haclose))
halow    = min(low,  min(haopen, haclose))

// HA colors
hacolor =  haclose  > haopen ? color.green : color.red

// Signals
turnGreen = haclose  >  haopen and haclose[1] <= haopen[1]
turnRed   = haclose  <= haopen and haclose[1]  > haopen[1]

plotshape(turnGreen, style = shape.arrowup,   location = location.belowbar, color = color.green, size=size.large)
plotshape(turnRed,   style = shape.arrowdown, location = location.abovebar, color = color.red, size=size.large) 


// Conditions
timePeriod = time >= fromDate
notInTrade = strategy.position_size == 0
goLong = turnGreen and notInTrade and timePeriod
goShort = turnRed and notInTrade and timePeriod

// Determine trail stop loss prices
longStopPrice = 0.0, shortStopPrice = 0.0

longStopPrice := if (strategy.position_size > 0)
    stopValue = low * (1 - trailingStopPerc)
    max(stopValue, longStopPrice[1])
else
    0

shortStopPrice := if (strategy.position_size < 0)
    stopValue = high * (1 + trailingStopPerc)
    min(stopValue, shortStopPrice[1])
else
    999999
        
// Long entries
strategy.entry(id = "long", long = strategy.long, when = goLong)
strategy.exit("exit", "long", stop=longStopPrice)

// Short entries
strategy.entry(id = "short", long = strategy.short, when = goShort)
strategy.exit("exit", "short", stop=shortStopPrice)


// Plot stop loss values for confirmation
longTrailingStopLine = plot(series=(showTrailingStopLine == "Yes" and strategy.position_size > 0) ? longStopPrice : na,
     color=color.red, style=plot.style_stepline,
     linewidth=1, title="Long Trail Stop")
     
shortTrailingStopLine = plot(series=(showTrailingStopLine == "Yes" and strategy.position_size < 0) ? shortStopPrice : na,
     color=color.red, style=plot.style_stepline,
     linewidth=1, title="Short Trail Stop")
     
// Entry line
entryPlot = plot(low, color=color.rgb(0, 0, 0, 100), style=plot.style_linebr, linewidth=1)

// SL area fill
fill(longTrailingStopLine, entryPlot, color=color.red)
fill(entryPlot, shortTrailingStopLine, color=color.red)

    